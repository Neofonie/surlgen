options {
    STATIC = false ;
}

PARSER_BEGIN(UrlMappingParser)
package de.neofonie.surlgen.urlmapping.parser;

import de.neofonie.surlgen.urlmapping.mapping.Mapping;
import de.neofonie.surlgen.urlmapping.mapping.MappingConfig;

import java.io.StringReader;
import java.util.ArrayList;
import java.util.List;

public class UrlMappingParser {

    public static UrlPattern parse(MappingConfig mappingConfig, String expression) throws ParseException, TokenMgrError {
        UrlMappingParser parser = new UrlMappingParser( new StringReader(expression) ) ;
        return parser.start(mappingConfig);
    }
}
PARSER_END(UrlMappingParser)

//http://eriklievaart.com/blog/javacc2.html

SKIP:  { " " | "\t" | "\n" | "\r"                    }
TOKEN: {
// < PARAM : (["a"-"z", "A"-"Z"])+ > |
 < MAPS: "{">: MAPPING
 | < ANYTHING : (~["{", "}", "[", "]"])+ >
}

<MAPPING> TOKEN: {
    < PARAM : (["a"-"z", "A"-"Z", "0"-"9"])+ >
    | <MAP : ":">
    | <END : "}">:DEFAULT
}

private UrlPattern start(MappingConfig mappingConfig): {UrlPattern t;} {
    t = tokenList(mappingConfig) <EOF>
    {return t;}
}

private UrlPattern tokenList(MappingConfig mappingConfig): {UrlPattern p; List<UrlPattern> list = new ArrayList();} {
    ( p = token(mappingConfig) { list.add(p); } )+
    { return PatternList.create(list); }
}

private UrlPattern token(MappingConfig mappingConfig): {Token t; UrlPattern p;} {
    ( "[" p = tokenList(mappingConfig) "]" { return new Choice(p); }
    | "{" p = mapping(mappingConfig) <END> { return p; }
    | p = anything() { return p; }
    )
}

private UrlPattern choice(): {UrlPattern p;} {
    "[" p = anything() "]" { return new Choice(p); }
}

private UrlPattern mapping(MappingConfig mappingConfig): {Token name;Token type;} {
    name = <PARAM> <MAP> type = <PARAM> { return new MappingPattern(name.image, type.image, mappingConfig); }
}

private UrlPattern anything(): {Token t; StringBuilder sb = new StringBuilder();} {
 t = <ANYTHING>
 { return new StaticUrlPattern(t.image); }
}